//行の一番最初に//を書くとその行がコメントになります
//最初でなければなりません

//行を空けても大丈夫です

//変数を定義する必要があります
str url
str path="Data/_NetDownloadData/"
//文字列は""で囲んでください。文字列に「"」そのものを入れたい場合は「\"」を入れてください
//しかし、\による他の特殊文字変換（お馴染みの改行など）はサポート対象外です
//なお、文字列は1行に限りますが、ウディタ変数アクセス（後述）はその限りではない

int remoteVersion
int localVersion
int prompt
//変数名の決まりはC/C++仕様です
//数字、英字と_を使えますが1文字目は数字以外でなければなりません

url="https://saltfish.moe/testserver/"
//url="\\cdb[26:0:1]/" <--\\を\に入れ替えてこういう風に入れても大丈夫です、たぶん
//この場合、\cdb[26:0:1]はこの行が実行される時ではなく「一番最初に、このスクリプトファイルを丸ごと読込む時」に変換されます
//つまりこの特殊文字の表す数値は読み込み時のものに固定され、「変数」として使うことはできません
//可変DBタイプ26が存在しない場合は緑色エラーウインドーが出ます

echo "バージョン更新します"
remoteVersion=dl(url+"version")
//返す値がある場合、入力内容を()で囲む必要がありますが、
//ない場合は囲まないでください。
//計算式も処理できますが、一回に一つだけ。1+2+3みたいな連続なのは駄目です。
//var=1+2とvar=var+3という風に分けてください。

//今回のremoteVersionの値は多分19061300となっています。
//いろいろテストしたい場合はlocalVersionをいじっちゃってください

localVersion=19061200
//localVersion=_i_1326000000 <--こういう風に入れても大丈夫です
//ウディタ変数を呼び出します。
//1326000000は変数呼び出し値で、_i_は「それが数字変数です」という意味です（iはinteger）
//文字列の場合は_s_（sはstring）。この文字列は複数行の可能性がありますのでご注意を
//「_i_1326000000」と「\sdb[26:0:0]」は同じDB位置を表していますが、
//「\sdb[26:0:0]」と違って、「_i_1326000000」は「この行が実行される時」に変換されます

if remoteVersion<localVersion
//条件を()で囲まないでください
//なお、条件も計算式と同じで一回に一つだけ…ご迷惑お掛けしました

    msgbox "もう最新バージョンです"
    return "latest"
	
else if remoteVersion==localVersion
//「上記以外」はelseとなっています。elseだけで使っても、else if という風に使っても大丈夫です
//条件判断の「一致」は「=」ではなく「==」となっています。そこもC/C++仕様です
//条件分岐は多重OKです

        loop
		//繰り返しは多重OKになりました
		    echo "test"+localVersion
			localVersion=localVersion-1
			if localVersion<=19061000
			    break
				//breakは繰り返しから脱出という意味です
			endif
			//一つのifにつき、必ず一つとendifが要ります。しつこいですが我慢して頂けると…
			//前述の「else if」も立派なifであるため、それも数えられます
			//endとifの間にスペースがありません
		next
		//繰り返しポイントはnextとなっています
    endif
endif

prompt=msgbox("更新しますか？")
//msgboxは選択肢機能があります。以下のいずれを満たせば選択肢が出ます
//1. 返す値がある；2. 選択肢テキストが設定されている。
//選択肢テキストは2番・3番の入力値で設定できます。
//省略すると自動的に「Yes」と「No」になります。

if prompt!=1
	exit
	//exitは値を返さず中断する意味です。入力値のないreturnと一緒です
endif

dl url+"TestData.mp3",path+"TestData.mp3"
//まあ、とりあえず聞いとけや……

if _i_9000048==1
//ここは\sys[48]と書いちゃだめです。なぜでしょうか？
    msgbox "更新成功しました"
else
    msgbox "更新失敗しました"
endif

return _i_9000048
//returnは値を返す意味です。数字でも文字列でも構いません。
//「return」だけ書いてればexitと一緒です